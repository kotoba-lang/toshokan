(ns toshokan.iccu-it-test
  (:require ["node:fs" :as fs]
            [clojure.test :refer [deftest is]]
            [toshokan.sources.iccu-it :as iccu]))

(def sample (js->clj (js/JSON.parse (fs/readFileSync "test/fixtures/iccu-it-sbn-sample.json" "utf8"))
                      :keywordize-keys true))

(deftest parses-all-records
  (let [recs (iccu/parse-response sample)]
    (is (= 5 (count recs)))
    (is (every? #(re-matches #"^iccu-it:.+" (:entity %)) recs))
    (is (every? :title recs))))

(deftest sanitizes-backslash-id-and-extracts-year
  (let [r (second (iccu/parse-response sample))]
    (is (= "iccu-it:IT-ICCU-RMS-2566836" (:entity r)))
    (is (= "1957" (:date r)))
    (is (= ["Natsume, Sōseki"] (:creators r)))))

(deftest handles-missing-year-gracefully
  (let [r (first (iccu/parse-response sample))]
    ;; real fixture: "Tōkyō : Iwanami shoten" has no year in it
    (is (nil? (:date r)))))

(deftest quads-cover-required-attrs
  (let [r (second (iccu/parse-response sample))
        quads (iccu/->quads 1 "2026-07-19T00:00:00Z" r)
        attrs (set (map second quads))]
    (is (contains? attrs :library/title))
    (is (contains? attrs :library/source))
    (is (contains? attrs :library/date))))
