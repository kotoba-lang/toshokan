(ns toshokan.ndl-test
  (:require ["node:fs" :as fs]
            [clojure.test :refer [deftest is testing]]
            [toshokan.sources.ndl :as ndl]))

(def sample-xml (fs/readFileSync "test/fixtures/ndl-sru-sample.xml" "utf8"))
(def multi-xml (fs/readFileSync "test/fixtures/ndl-sru-multi-sample.xml" "utf8"))

(deftest parses-single-record
  (let [recs (ndl/parse-records sample-xml)]
    (is (= 1 (count recs)))
    (let [r (first recs)]
      (is (= "ndl:R100000001-I11141124078689" (:entity r)))
      (is (= "青い目の坊っちゃん" (:title r)))
      (is (= ["ジョン・ストッカー" "宇野, 輝雄"] (:creators r)))
      (is (= ["早川書房"] (:publishers r)))
      (is (= "1970" (:issued r)))
      (is (= "935" (:ndc r))))))

(deftest parses-multiple-records
  (let [recs (ndl/parse-records multi-xml)]
    (is (= 8 (count recs)))
    (is (every? :entity recs))
    (is (every? :title recs))
    (is (every? #(re-matches #"^ndl:.+" (:entity %)) recs))))

(deftest quads-cover-required-attrs
  (let [r (first (ndl/parse-records sample-xml))
        quads (ndl/->quads 1 "2026-07-19T00:00:00Z" r)
        attrs (set (map second quads))]
    (is (contains? attrs :library/title))
    (is (contains? attrs :library/source))
    (is (contains? attrs :library/retrieved-at))
    (is (every? #(= "ndl:R100000001-I11141124078689" (first %)) quads))
    (is (every? #(= 1 (nth % 3)) quads))
    (is (every? #(= :add (nth % 4)) quads))))
