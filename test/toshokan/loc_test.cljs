(ns toshokan.loc-test
  (:require ["node:fs" :as fs]
            [clojure.test :refer [deftest is]]
            [clojure.string :as str]
            [toshokan.sources.loc :as loc]))

(defn- nfc [s] (.normalize s "NFC"))

(def sample (js->clj (js/JSON.parse (fs/readFileSync "test/fixtures/loc-search-sample.json" "utf8"))
                      :keywordize-keys true))

(deftest parses-response
  (let [recs (loc/parse-response sample)]
    (is (= 1 (count recs)))
    (let [r (first recs)]
      (is (= "loc:2011528232" (:entity r)))
      (is (str/starts-with? (nfc (:title r)) (nfc "Eibun Gakusha, Natsume S")))
      (is (= ["2011528232"] (:lccn r)))
      (is (= "https://lccn.loc.gov/2011528232" (:source-url r))))))

(deftest quads-cover-required-attrs
  (let [r (first (loc/parse-response sample))
        quads (loc/->quads 1 "2026-07-19T00:00:00Z" r)
        attrs (set (map second quads))]
    (is (contains? attrs :library/title))
    (is (contains? attrs :library/lccn))
    (is (contains? attrs :library/source))
    (is (every? #(= "loc:2011528232" (first %)) quads))))
