(ns toshokan.kb-nl-test
  (:require ["node:fs" :as fs]
            [clojure.test :refer [deftest is]]
            [clojure.string :as str]
            [toshokan.sources.kb-nl :as kb-nl]))

(def sample-xml (fs/readFileSync "test/fixtures/kb-nl-sru-sample.xml" "utf8"))

(deftest parses-all-records-despite-schema-variance
  ;; real fixture: 3 records, one of which lacks a dc:identifier[URI] and
  ;; only has dcx:recordIdentifier with a different PPN=<n> URL shape --
  ;; regression guard for that fallback.
  (let [recs (kb-nl/parse-records sample-xml)]
    (is (= 3 (count recs)))
    (is (every? #(re-matches #"^kb-nl:\d+$" (:entity %)) recs))))

(deftest resolver-kb-nl-identifier-shape
  (let [r (first (kb-nl/parse-records sample-xml))]
    (is (= "kb-nl:403980046" (:entity r)))
    (is (= "Kokoro" (:title r)))
    (is (= ["9789048836109"] (:isbn r)))
    (is (= "dut" (:language r)))))

(deftest picarta-recordidentifier-fallback-shape
  (let [r (second (kb-nl/parse-records sample-xml))]
    (is (= "kb-nl:401343014" (:entity r)))
    (is (str/starts-with? (:source-url r) "http://picarta.pica.nl"))))

(deftest quads-cover-required-attrs
  (let [r (first (kb-nl/parse-records sample-xml))
        quads (kb-nl/->quads 1 "2026-07-19T00:00:00Z" r)
        attrs (set (map second quads))]
    (is (contains? attrs :library/title))
    (is (contains? attrs :library/source))
    (is (contains? attrs :library/isbn))))
